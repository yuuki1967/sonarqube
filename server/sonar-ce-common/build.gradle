description = 'Code relating to the Compute Engine shared between the Compute Engine and the Web Server'

sonarqube {
  properties {
    property 'sonar.projectName', "${projectTitle} :: Compute Engine :: Common"
  }
}

sourceSets {
  test {
    resources {
      srcDirs += ['src/test/projects']
    }
  }
}


import org.apache.tools.ant.filters.ReplaceTokens
processResources {
  filesMatching('build.properties') {
    filter ReplaceTokens, tokens: [
      'buildNumber': release ? 'git rev-parse HEAD'.execute().text.trim() : 'N/A'
    ]
  }
}

configurations {
  testCompile.extendsFrom compileOnly
}

dependencies {
  // please keep the list grouped by configuration and ordered by name

  compile 'org.slf4j:jul-to-slf4j'
  compile 'org.slf4j:slf4j-api'
  compile project(':server:sonar-db-dao')

  compileOnly 'com.google.code.findbugs:jsr305'
  compileOnly project(':server:sonar-ce-task')
  compileOnly project(':server:sonar-server-common')

  testCompile 'com.google.code.findbugs:jsr305'
  testCompile 'com.h2database:h2'
  testCompile 'com.tngtech.java:junit-dataprovider'
  testCompile 'junit:junit'
  testCompile 'org.apache.logging.log4j:log4j-api'
  testCompile 'org.apache.logging.log4j:log4j-core'
  testCompile 'org.assertj:assertj-core'
  testCompile 'org.assertj:assertj-guava'
  testCompile 'org.mockito:mockito-core'
  testCompile project(':sonar-plugin-api')
  testCompile project(':sonar-core')
  testCompile project(':server:sonar-db-testing')
  testCompile project(path: ":server:sonar-server-common", configuration: "tests")
}
