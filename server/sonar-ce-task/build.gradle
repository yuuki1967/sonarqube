description = 'Definition of a Compute Engine task and utility "framework" and classes to code one'

sonarqube {
  properties {
    property 'sonar.projectName', "${projectTitle} :: Compute Engine :: Task"
  }
}

sourceSets {
  test {
    resources {
      srcDirs += ['src/test/projects']
    }
  }
}


import org.apache.tools.ant.filters.ReplaceTokens
processResources {
  filesMatching('build.properties') {
    filter ReplaceTokens, tokens: [
      'buildNumber': release ? 'git rev-parse HEAD'.execute().text.trim() : 'N/A'
    ]
  }
}

dependencies {
  // please keep the list grouped by configuration and ordered by name

  compile 'org.picocontainer:picocontainer'
  compile 'org.slf4j:jul-to-slf4j'
  compile 'org.slf4j:slf4j-api'

  compile project(':server:sonar-process')
  compile project(':server:sonar-server-common')
  compile project(':sonar-core')
  compileOnly project(path: ':sonar-plugin-api')

  compileOnly 'com.google.code.findbugs:jsr305'

  testCompile 'ch.qos.logback:logback-access'
  testCompile 'ch.qos.logback:logback-classic'
  testCompile 'ch.qos.logback:logback-core'
  testCompile 'com.google.code.findbugs:jsr305'
  testCompile 'com.h2database:h2'
  testCompile 'com.tngtech.java:junit-dataprovider'
  testCompile 'junit:junit'
  testCompile 'org.apache.logging.log4j:log4j-api'
  testCompile 'org.apache.logging.log4j:log4j-core'
  testCompile 'org.assertj:assertj-core'
  testCompile 'org.assertj:assertj-guava'
  testCompile 'org.mockito:mockito-core'
  testCompile 'org.reflections:reflections'
  testCompile project(':server:sonar-db-testing')
}

task testJar(type: Jar) {
  classifier = 'tests'
  from sourceSets.test.output
}

configurations {
  tests
}

artifacts {
 tests testJar
}
