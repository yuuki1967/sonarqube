sonarqube {
  properties {
    property 'sonar.projectName', "${projectTitle} :: Scanner Engine"
  }
}

// Excluding scanner files in order to benefit of incremental build support
sourceSets.test.resources {
  exclude '**/.sonar/**'
}

configurations {
  testCompile.extendsFrom(compileOnly)
}

dependencies {
  // please keep the list ordered

  compile 'commons-codec:commons-codec'
  compile 'commons-lang:commons-lang'
  compile 'com.google.code.gson:gson'
  compile 'com.google.guava:guava'
  compile 'org.apache.commons:commons-csv'
  compile 'org.freemarker:freemarker'
  compile 'org.slf4j:jcl-over-slf4j'
  compile 'org.slf4j:jul-to-slf4j'
  compile 'org.slf4j:log4j-over-slf4j'
  compile 'org.slf4j:slf4j-api'
  compile project(':sonar-core')
  compile project(':sonar-scanner-protocol')
  compile project(':sonar-ws')
  compile project(':sonar-duplications')
  runtime project(path: ':sonar-plugin-api', configuration: 'shadow')
  compileOnly project(path: ':sonar-plugin-api')

  compileOnly 'com.google.code.findbugs:jsr305'

  testCompile 'com.squareup.okhttp3:mockwebserver'
  testCompile 'com.tngtech.java:junit-dataprovider'
  testCompile 'javax.servlet:javax.servlet-api'
  testCompile 'junit:junit'
  testCompile 'net.javacrumbs.json-unit:json-unit-assertj:0.0.15'
  testCompile 'org.assertj:assertj-core'
  testCompile 'org.mockito:mockito-core'
  testCompile project(':plugins:sonar-xoo-plugin')
  testCompile project(':sonar-plugin-api').sourceSets.test.output
}

license {
  excludes(["**/Fake.java", "**/Fake.groovy", "org/sonar/scanner/cpd/ManyStatements.java"])
}

artifactoryPublish.skip = false

// Used by core plugins
publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      if (release) {
        artifact sourcesJar
        artifact javadocJar
      }
    }
  }
}
